// Rust counterpart: `src/shaders/common.rs`
struct FrameUniforms {
    projection: mat4x4<f32>,
    inverse_projection: mat4x4<f32>,
    view: mat4x4<f32>,
    inverse_view: mat4x4<f32>,
    light_transform: mat4x4<f32>,
    resolution: vec2<f32>,
    fog_density: f32,
    fog_distance: f32,
    fog_color: u32,
    sky_color: u32,
    flags: u32,
    milliseconds: u32,
    sun_direction: vec3<f32>,
    fog_height: f32,
}

@group(0) @binding(0)
var<uniform> frame: FrameUniforms;

// The structure that's interpolated accross the trangles
// generated by the vertex shader.
struct Interpolator {
    // The position of the vertex in clip-space coordinates.
    @builtin(position) position: vec4<f32>,
    // The direction that the camera is facing in world-space coordinates.
    @location(0) eye_direction: vec3<f32>,
}

// Turns a 4x4 matrix into a 3x3 matrix by extracting the rotation and scale
// components.
fn extract_rotation_scale(m: mat4x4<f32>) -> mat3x3<f32> {
    return mat3x3<f32>(
        vec3<f32>(m[0].xyz),
        vec3<f32>(m[1].xyz),
        vec3<f32>(m[2].xyz),
    );
}

@vertex
fn vs_main(
    @builtin(vertex_index) vertex_index: u32,
) -> Interpolator {
    let uv = vec2<f32>(f32(vertex_index & 1u), f32(vertex_index >> 1u)) * 2.0 - 1.0;

    var eye_pos = frame.inverse_view * frame.inverse_projection * vec4<f32>(uv, 1.0, 1.0);
    eye_pos /= eye_pos.w;

    var out: Interpolator;
    out.position = vec4<f32>(uv, 0.0, 1.0);
    out.eye_direction = eye_pos.xyz;
    return out;
}

// Unpacks the provided color.
fn unpack_color(color: u32) -> vec4<f32> {
    return vec4<f32>(
        f32((color >> 24u) & 0xFFu) / 255.0,
        f32((color >> 16u) & 0xFFu) / 255.0,
        f32((color >> 8u) & 0xFFu) / 255.0,
        f32(color & 0xFFu) / 255.0,
    );
}

// Compute the intensity of the sun for the given pixel.
fn sun(eye_dir: vec3<f32>) -> f32 {
    var value = max(0.0, dot(eye_dir, -frame.sun_direction));
    return pow(value, 300.0);
}

fn sky(eye_dir: vec3<f32>) -> vec4<f32> {
    if (eye_dir.y > 0.0) {
        return mix(unpack_color(frame.fog_color), unpack_color(frame.sky_color), eye_dir.y);
    } else {
        return unpack_color(frame.fog_color);
    }
}

@fragment
fn fs_main(
    in: Interpolator,
) -> @location(0) vec4<f32> {
    let eye_dir = normalize(in.eye_direction);
    let sky_color = sky(eye_dir);
    let sun_intencity = sun(eye_dir);
    return sky_color + vec4<f32>(1.0, 1.0, 1.0, 1.0) * sun_intencity;
}
